plugins {
    id 'java-platform'
    id 'maven-publish'
}

dependencies {
    constraints {
        (parent.subprojects - project)
                .sort { "$it.name" }
                .each {api it }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.javaPlatform
            pom {
                name = 'Shakespeare BOM'
                description = 'Bill of Matrial (BOM) for the Shakespeare framework.'
                url = 'https://github.com/mkutz/shakespeare'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mkutz'
                        name = 'Michael Kutz'
                        email = 'mail@michael-kutz.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mkutz/shakespeare.git'
                    developerConnection = 'scm:git:ssh://github.com/mkutz/shakespeare.git'
                    url = 'https://github.com/mkutz/shakespeare'
                }
            }
        }
    }
}
