plugins {
  id 'java-library'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
  id 'shakespeare.style'
  id 'shakespeare.testing'
}

java {
  withJavadocJar()
  withSourcesJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        url = 'https://github.com/mkutz/shakespeare'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'mkutz'
            name = 'Michael Kutz'
            email = 'mail@michael-kutz.de'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/mkutz/shakespeare.git'
          developerConnection = 'scm:git:ssh://github.com/mkutz/shakespeare.git'
          url = 'https://shakespeareframework.org'
        }
      }
    }
  }
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { !version.endsWith('SNAPSHOT') }
  useInMemoryPgpKeys(findProperty('signingKey'), findProperty('signingPassword'))
  sign publishing.publications.mavenJava
}
